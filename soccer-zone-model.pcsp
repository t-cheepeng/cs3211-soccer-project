// TODO List (In order of priority, Highest -> Lowest):
// 1. Assign probabilities to success/failure. Probably do this with a C# Helper class again
// 2. Expand to larger number of zones and larger number of players - Done but too slow with 11 players

/*
 * ========================
 *           MODEL
 * ========================
 * The playing field is modeled as a 2D array with an X-axis (forward-backward) and Y-axis (left-right) value. 
 * N_X = 5 captures the max size of the X-axis and N_Y = 3 captures the max size of the Y-axis.
 * Each element of the 2D array is defined as a Zone. i.e. The playing field (0, 0) is a Zone. 
 * 
 * -----------------------
 *  Visual Representation
 * -----------------------
 *        Team0's
 *         Goal
 * +=====+=====+=====+
 * | 0,0 | 0,1 | 0,2 |
 * +=====+=====+=====+
 * | 1,0 | 1,1 | 1,2 |
 * +=====+=====+=====+
 * | 2,0 | 2,1 | 2,2 |
 * +=====+=====+=====+
 * | 3,0 | 3,1 | 3,2 |
 * +=====+=====+=====+
 * | 4,0 | 4,1 | 4,2 |
 * +=====+=====+=====+
 *       Team1's
 *        Goal
 *
 * -----------------------
 *     Shared Variables
 * -----------------------
 * team0StartFormation     - Represents the start formation of Team0 by setting the number of players in each zone. 
 *                           Zone (0, 1) must have at least one player (acting as the goalkeeper)
 *                           Zone (2, 1) must have at least one player (for kickoff)
 * team1StartFormation     - Represents the start formation of Team1 by setting the number of players in each zone. 
 *                           Zone (4, 1) must have at least one player (acting as the goalkeeper)
 *                           Zone (2, 1) must have at least one player (for kickoff)
 * time                    - Represents the number of time units for a game. 
 *                           Once the time reaches 0, the game is over.
 * teamInPossessionOfBall  - Represents the team in possession of the ball. 
 *                           Can only be 0 or 1.
 * ballPosX                - Represents the X-axis position of the ball on the playing field. 
 *                           Can take values from 0 to 4.
 *                           Initial value is not important as it will be set by the KickOff process.
 * ballPosY                - Represents the Y-axis position of the ball on the playing field. 
 *                           Can take values from 0 to 2.
 *                           Initial value is not important as it will be set by the KickOff process.
 * numOfTeam0PlayersInZone - Represents the number of Team0 players on the playing field. 
 *                           Initial value is not important as it will be set equivalent to team0StartFormation by the ResetPlayerPositions process.
 *                           Zone (0, 1) must have at least one player (acting as the goalkeeper)
 * numOfTeam1PlayersInZone - Represents the number of Team1 players on the playing field. 
 *                           Initial value is not important as it will be set equivalent to team1StartFormation by the ResetPlayerPositions process.
 *                           Zone (4, 1) must have at least one player (acting as the goalkeeper)
 * score                   - Represents the score of each team. 
 *
 * -----------------------
 *        Processes
 * -----------------------
 * PlayGame()
 * Represents a full soccer game using the zone model approach as describe earlier.
 *
 * ResetPlayerPositions()
 * Reset both Teams' position back to the starting formation. 
 *
 * KickOff(teamNum)
 * Start the game by placing the ball in the middle of the field and giving teamNum the possession of the ball. 
 *
 * TickTock()
 * Decrement time by one unit and continue to simulate a single action by a single zone.
 * This process will be called at the end of each action until game is over (time == 0).
 * 
 * Zone(zoneX, zoneY)
 * Captures both Team0 and Team1 actions that a Zone can perform.
 * The actions are guarded by a guard clause that checks if the particular zone can perform an action.
 * When game is over, all Zones will not be able to perform any actions.
 *
 * Actions(teamNum, zoneX, zoneY)
 * Captures all actions that a given team can perform in a given Zone.
 * It is guarded by a guard clause that checks if the team can perform any actions in the given zone.
 * i.e. Only when the chance of performing particular action is non-zero for at least one action
 * Possible Actions: Shoot, Dribble, Pass, Run
 *
 * Shoot(teamNum, zoneX, zoneY)
 * 
 * ShootSuccess(teamNum, zoneX, zoneY)
 *
 * ShootFail(teamNum, zoneX, zoneY)
 *
 * Dribble(teamNum, zoneX, zoneY)
 * 
 * DribbleTo(teamNum, zoneX, zoneY, toZoneX, toZoneY)
 *
 * DribbleSuccess(teamNum, zoneX, zoneY, toZoneX, toZoneY)
 *
 * DribbleFail(teamNum, zoneX, zoneY, toZoneX, toZoneY)
 *
 * Pass(teamNum, zoneX, zoneY)
 *
 * PassTo(teamNum, zoneX, zoneY, toZoneX, toZoneY)
 *
 * PassSuccess(teamNum, zoneX, zoneY, toZoneX, toZoneY)
 *
 * PassFail(teamNum, zoneX, zoneY, toZoneX, toZoneY)
 *
 * Run(teamNum, zoneX, zoneY)
 *
 * RunTo(teamNum, zoneX, zoneY, toZoneX, toZoneY)
 */

#import "PAT.Lib.SoccerStateHelper";

#define timeOver time == 0;
#define N_X 5; // Forward-backward axis
#define N_Y 3; // Sideway axis

hvar team0StartFormation[N_X][N_Y] = [
	0, 1, 0,
	1, 1, 1,
	1, 1, 1,
	0, 0, 0,
	0, 0, 0
];
hvar team1StartFormation[N_X][N_Y] = [
	0, 0, 0,
	0, 0, 0,
	1, 1, 1,
	1, 1, 1,
	0, 1, 0
];
var time = 5;
var teamInPossessionOfBall:{0..1} = 0;
var ballPosX:{0..N_X-1} = 0;
var ballPosY:{0..N_Y-1} = 0;
var numOfTeam0PlayersInZone[N_X][N_Y] = [
	0, 0, 0,
	0, 0, 0,
	0, 0, 0,
	0, 0, 0,
	0, 0, 0
];
var numOfTeam1PlayersInZone[N_X][N_Y] = [
	0, 0, 0,
	0, 0, 0,
	0, 0, 0,
	0, 0, 0,
	0, 0, 0
];
var score = [0, 0];


// ========== Soccer Match ========== //
// Main game
PlayGame = ResetPlayerPositions; pcase{1: KickOff(0) 1: KickOff(1)}; TickTock;



// ========== Utils Process ========== //
// Reset player positions
ResetPlayerPositions = reset{
	numOfTeam0PlayersInZone[0][0] = team0StartFormation[0][0];
	numOfTeam0PlayersInZone[0][1] = team0StartFormation[0][1];
	numOfTeam0PlayersInZone[0][2] = team0StartFormation[0][2];
	numOfTeam0PlayersInZone[1][0] = team0StartFormation[1][0];
	numOfTeam0PlayersInZone[1][1] = team0StartFormation[1][1];
	numOfTeam0PlayersInZone[1][2] = team0StartFormation[1][2];
	numOfTeam0PlayersInZone[2][0] = team0StartFormation[2][0];
	numOfTeam0PlayersInZone[2][1] = team0StartFormation[2][1];
	numOfTeam0PlayersInZone[2][2] = team0StartFormation[2][2];
	numOfTeam0PlayersInZone[3][0] = team0StartFormation[3][0];
	numOfTeam0PlayersInZone[3][1] = team0StartFormation[3][1];
	numOfTeam0PlayersInZone[3][2] = team0StartFormation[3][2];
	numOfTeam0PlayersInZone[4][0] = team0StartFormation[4][0];
	numOfTeam0PlayersInZone[4][1] = team0StartFormation[4][1];
	numOfTeam0PlayersInZone[4][2] = team0StartFormation[4][2];

	numOfTeam1PlayersInZone[0][0] = team1StartFormation[0][0];
	numOfTeam1PlayersInZone[0][1] = team1StartFormation[0][1];
	numOfTeam1PlayersInZone[0][2] = team1StartFormation[0][2];
	numOfTeam1PlayersInZone[1][0] = team1StartFormation[1][0];
	numOfTeam1PlayersInZone[1][1] = team1StartFormation[1][1];
	numOfTeam1PlayersInZone[1][2] = team1StartFormation[1][2];
	numOfTeam1PlayersInZone[2][0] = team1StartFormation[2][0];
	numOfTeam1PlayersInZone[2][1] = team1StartFormation[2][1];
	numOfTeam1PlayersInZone[2][2] = team1StartFormation[2][2];
	numOfTeam1PlayersInZone[3][0] = team1StartFormation[3][0];
	numOfTeam1PlayersInZone[3][1] = team1StartFormation[3][1];
	numOfTeam1PlayersInZone[3][2] = team1StartFormation[3][2];
	numOfTeam1PlayersInZone[4][0] = team1StartFormation[4][0];
	numOfTeam1PlayersInZone[4][1] = team1StartFormation[4][1];
	numOfTeam1PlayersInZone[4][2] = team1StartFormation[4][2];
} -> Skip;

// Kickoff
KickOff(teamNum) = kickOff.teamNum{teamInPossessionOfBall = teamNum; ballPosX = 2; ballPosY = 1;} -> Skip;

// Decrement time and let a single zone perform an action
TickTock = tau{time--} -> ([!timeOver]([]x:{0..N_X-1};y:{0..N_Y-1} @ Zone(x, y)) [] [timeOver] Skip);



// ========== Zone ========== //
Zone(zoneX, zoneY) = 
[call(canZoneAct, numOfTeam0PlayersInZone[zoneX][zoneY] + numOfTeam1PlayersInZone[zoneX][zoneY], zoneX, zoneY, ballPosX, ballPosY)] ([] teamNum:{0..1} @ Actions(teamNum, zoneX, zoneY)); TickTock; // Perform an action for a zone that can act

Actions(teamNum, zoneX, zoneY) = 
[call(canTeamZoneAct, teamNum, zoneX, zoneY, teamInPossessionOfBall, ballPosX, ballPosY, numOfTeam0PlayersInZone, numOfTeam1PlayersInZone)] pcase {
	call(shootActionRate  , teamNum, zoneX, zoneY, teamInPossessionOfBall, ballPosX, ballPosY, numOfTeam0PlayersInZone, numOfTeam1PlayersInZone) : Shoot  (teamNum, zoneX, zoneY)
	call(dribbleActionRate, teamNum, zoneX, zoneY, teamInPossessionOfBall, ballPosX, ballPosY, numOfTeam0PlayersInZone, numOfTeam1PlayersInZone) : Dribble(teamNum, zoneX, zoneY)
	call(passActionRate   , teamNum, zoneX, zoneY, teamInPossessionOfBall, ballPosX, ballPosY, numOfTeam0PlayersInZone, numOfTeam1PlayersInZone) : Pass   (teamNum, zoneX, zoneY)
	call(runActionRate    , teamNum, zoneX, zoneY, teamInPossessionOfBall, ballPosX, ballPosY, numOfTeam0PlayersInZone, numOfTeam1PlayersInZone) : Run    (teamNum, zoneX, zoneY)
};



// ############################# //
// ========== Actions ========== //
// ############################# //

// ========== Shoots =========== //
Shoot(teamNum, zoneX, zoneY) = pcase {
	call(shootSuccessRate, zoneX, zoneY, teamNum): ShootSuccess(teamNum, zoneX, zoneY)
	call(shootFailRate, zoneX, zoneY, teamNum): ShootFail(teamNum, zoneX, zoneY) 
};

// Success:	Increment score, switch possession, reset player pos
ShootSuccess(teamNum, zoneX, zoneY) = shootSuccess.teamNum.zoneX.zoneY{
	score[teamNum]++; 
} -> ResetPlayerPositions; KickOff(1 - teamNum);

// Fail: Switch possession, start ball at opponent goal zone
ShootFail(teamNum, zoneX, zoneY) = shootFail.teamNum.zoneX.zoneY{
	if (teamNum == 0) {
		teamInPossessionOfBall = 1;
		ballPosX = N_X - 1;
		ballPosY = 1;
	} else {
		teamInPossessionOfBall = 0;
		ballPosX = 0;
		ballPosY = 1;
	}
} -> Skip;



// ========== Dribble ========== //
// Can dribble forward/left/right
Dribble(teamNum, zoneX, zoneY) = 
if (teamNum == 0) {
	if (zoneX == N_X - 1) {
		DribbleTo(teamNum, zoneX, zoneY, zoneX  , zoneY+1) [] 
		DribbleTo(teamNum, zoneX, zoneY, zoneX  , zoneY-1)
	} else {
		DribbleTo(teamNum, zoneX, zoneY, zoneX+1, zoneY  ) [] 
		DribbleTo(teamNum, zoneX, zoneY, zoneX+1, zoneY+1) [] 
		DribbleTo(teamNum, zoneX, zoneY, zoneX+1, zoneY-1)
	}
} else {
	if (zoneX == 0) {
		DribbleTo(teamNum, zoneX, zoneY, zoneX  , zoneY+1) [] 
		DribbleTo(teamNum, zoneX, zoneY, zoneX  , zoneY-1)
	} else {
		DribbleTo(teamNum, zoneX, zoneY, zoneX-1, zoneY  ) []
		DribbleTo(teamNum, zoneX, zoneY, zoneX-1, zoneY+1) [] 
		DribbleTo(teamNum, zoneX, zoneY, zoneX-1, zoneY-1)
	}
}; 
DribbleTo(teamNum, zoneX, zoneY, toZoneX, toZoneY) = [call(canDribbleToZone, teamNum, zoneX, zoneY, numOfTeam0PlayersInZone, numOfTeam1PlayersInZone, toZoneX, toZoneY)] pcase {
	call(dribbleSuccessRate)                                                                        : DribbleSuccess(teamNum, zoneX, zoneY, toZoneX, toZoneY)
	call(dribbleFailRate,   teamNum, zoneX, zoneY, numOfTeam0PlayersInZone, numOfTeam1PlayersInZone): DribblePassFail(teamNum, zoneX, zoneY, toZoneX, toZoneY) 
};

// Success:	advance ball to toZone, advance one player to toZone
DribbleSuccess(teamNum, zoneX, zoneY, toZoneX, toZoneY) = dribbleSuccess.teamNum.zoneX.zoneY.toZoneX.toZoneY{
	ballPosX = toZoneX;
	ballPosY = toZoneY;
	if(teamNum == 0) {
		numOfTeam0PlayersInZone[zoneX][zoneY]--;
		numOfTeam0PlayersInZone[toZoneX][toZoneY]++;
	} else {
		numOfTeam1PlayersInZone[zoneX][zoneY]--;
		numOfTeam1PlayersInZone[toZoneX][toZoneY]++;
	}
} -> Skip;


// ========== Pass ========== //
// Can pass forward to any other zone with teammate
Pass(teamNum, zoneX, zoneY) =
if (teamNum == 0) {
	[] toZoneX:{zoneX..N_X-1};toZoneY:{0..N_Y-1} @ PassTo(teamNum, zoneX, zoneY, toZoneX, toZoneY)
} else {
	[] toZoneX:{0..zoneX};toZoneY:{0..N_Y-1} @ PassTo(teamNum, zoneX, zoneY, toZoneX, toZoneY)
};

PassTo(teamNum, zoneX, zoneY, toZoneX, toZoneY) = [call(canPassToZone, teamNum, zoneX, zoneY, numOfTeam0PlayersInZone, numOfTeam1PlayersInZone, toZoneX, toZoneY)] pcase {
	call(passSuccessRate)                                                                      : PassSuccess(teamNum, zoneX, zoneY, toZoneX, toZoneY)
	call(passFailRate, teamNum, zoneX, zoneY, numOfTeam0PlayersInZone, numOfTeam1PlayersInZone): DribblePassFail(teamNum, zoneX, zoneY, toZoneX, toZoneY) 
};

// Success: ball position change
PassSuccess(teamNum, zoneX, zoneY, toZoneX, toZoneY) = passSuccess.teamNum.zoneX.zoneY.toZoneX.toZoneY{
	ballPosX = toZoneX;
	ballPosY = toZoneY;
} -> Skip;

// ========== Dirbble/Pass Common Event ========== //
// Fail: Switch possession and transfer ball to some random player in opposing team
DribblePassFail(teamNum, zoneX, zoneY, toZoneX, toZoneY) = dribblePassFail.teamNum.zoneX.zoneY.toZoneX.toZoneY{
	var newBallPos = call(findNearestPlayerToTakeBall, teamInPossessionOfBall, numOfTeam0PlayersInZone, numOfTeam1PlayersInZone);
 	ballPosX = newBallPos[0];
 	ballPosY = newBallPos[1];
	if(teamNum == 0) {
		teamInPossessionOfBall = 1;
	} else {
		teamInPossessionOfBall = 0;
	}
} -> Skip;



// =========== Run ============ //
// Can only run forward/left/right
Run(teamNum, zoneX, zoneY) = 
if (teamNum == 0) {
	if (zoneX == N_X - 1) {
		RunTo(teamNum, zoneX, zoneY, zoneX  , zoneY+1) [] 
		RunTo(teamNum, zoneX, zoneY, zoneX  , zoneY-1)
	} else {
		RunTo(teamNum, zoneX, zoneY, zoneX+1, zoneY  ) [] 
		RunTo(teamNum, zoneX, zoneY, zoneX+1, zoneY+1) [] 
		RunTo(teamNum, zoneX, zoneY, zoneX+1, zoneY-1)
	}
} else {
	if (zoneX == 0) {
		RunTo(teamNum, zoneX, zoneY, zoneX  , zoneY+1) [] 
		RunTo(teamNum, zoneX, zoneY, zoneX  , zoneY-1)
	} else {
		RunTo(teamNum, zoneX, zoneY, zoneX-1, zoneY  ) []
		RunTo(teamNum, zoneX, zoneY, zoneX-1, zoneY+1) [] 
		RunTo(teamNum, zoneX, zoneY, zoneX-1, zoneY-1)
	}
}; 
RunTo(teamNum, zoneX, zoneY, toZoneX, toZoneY) = [call(canRunToZone, teamNum, zoneX, zoneY, numOfTeam0PlayersInZone, numOfTeam1PlayersInZone, toZoneX, toZoneY)] runSuccess.teamNum.zoneX.zoneY.toZoneX.toZoneY {
	if (teamNum == 0) {
		numOfTeam0PlayersInZone[zoneX][zoneY]--;
		numOfTeam0PlayersInZone[toZoneX][toZoneY]++;
	} else {
		numOfTeam1PlayersInZone[zoneX][zoneY]--;
		numOfTeam1PlayersInZone[toZoneX][toZoneY]++;
	}
} -> Skip;

// ========== Verifications ========== //
#define team0Win timeOver && score[0] > score[1] ;
#define team1Win timeOver && score[0] < score[1];
#define draw     timeOver && score[0] == score[0];

#assert PlayGame deadlockfree;
#assert PlayGame reaches team0Win with prob;
#assert PlayGame reaches team1Win with prob;
#assert PlayGame reaches draw with prob;
#assert PlayGame reaches timeOver with prob;